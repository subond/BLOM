#! /bin/csh -f 
cd $1
setenv CASE `./xmlquery -file env_case.xml CASE -value`
setenv CONFIG_OCN_FILE `./xmlquery -file env_case.xml CONFIG_OCN_FILE -value`
setenv CASEROOT `./xmlquery -file env_case.xml CASEROOT -value`
setenv SRCROOT `./xmlquery -file env_case.xml SRCROOT -value`
setenv EXEROOT `./xmlquery -file env_build.xml EXEROOT -value`
setenv OBJROOT `./xmlquery -file env_build.xml OBJROOT -value`
setenv OCN_GRID `./xmlquery -file env_build.xml OCN_GRID -value`
setenv CONFIG_OCN_DIR `dirname $CONFIG_OCN_FILE`
setenv NTASKS_OCN `./xmlquery -file env_mach_pes.xml NTASKS_OCN -value`
setenv DIN_LOC_ROOT `./xmlquery -file env_run.xml DIN_LOC_ROOT -value`
setenv RUN_TYPE `./xmlquery -file env_run.xml RUN_TYPE -value`
setenv CASEBUILD `./xmlquery -file env_case.xml CASEBUILD -value`
setenv CCSM_CO2_PPMV `./xmlquery -file env_run.xml CCSM_CO2_PPMV -value`
setenv OCN_NCPL `./xmlquery -file env_run.xml OCN_NCPL -value`
setenv MICOM_COUPLING `./xmlquery -file env_run.xml MICOM_COUPLING -value`
setenv RUNDIR `./xmlquery -file env_run.xml RUNDIR -value`
setenv MICOM_TRACER_MODULES `./xmlquery -file env_build.xml MICOM_TRACER_MODULES -value`
setenv MICOM_RIVER_NUTRIENTS `./xmlquery -file env_build.xml MICOM_RIVER_NUTRIENTS -value`
setenv MICOM_TURBULENT_CLOSURE `./xmlquery -file env_build.xml MICOM_TURBULENT_CLOSURE -value`
setenv GMAKE `./xmlquery -file env_build.xml GMAKE -value`
setenv GMAKE_J `./xmlquery -file env_build.xml GMAKE_J -value`
setenv MICOM_CO2_TYPE `./xmlquery -file env_run.xml MICOM_CO2_TYPE -value`
setenv RUN_STARTDATE `./xmlquery -file env_run.xml RUN_STARTDATE -value`

cd $OBJROOT/ocn/obj

#------------------------------------------------------------------------------
# Set list of file paths and resolve C preprocessor macros
#------------------------------------------------------------------------------

cat >! Filepath << EOF1
$OBJROOT/ocn/obj/dimensions
$SRCROOT/SourceMods/src.micom
$SRCROOT/components/micom/ben02
$SRCROOT/components/micom/cesm
$SRCROOT/components/micom/drivers/cpl_share
$SRCROOT/components/micom/drivers/cpl_mct
$SRCROOT/components/micom/phy
EOF1

set turbclo = (`echo $MICOM_TURBULENT_CLOSURE`)
set tracers = (`echo $MICOM_TRACER_MODULES`)
set co2type = (`echo $MICOM_CO2_TYPE`)
set rivnutr = (`echo $MICOM_RIVER_NUTRIENTS`)

set cpp_ocn = "-DMPI"
if ($OCN_GRID == tnx2v1 || $OCN_GRID == tnx1v1 || $OCN_GRID == tnx1.5v1 || $OCN_GRID == tnx0.25v1) then
  set cpp_ocn = "$cpp_ocn -DARCTIC"
endif
if ($OCN_GRID == gx1v5 || $OCN_GRID == gx1v6 || $OCN_GRID == tnx1v1 || $OCN_GRID == tnx0.25v1) then
  set cpp_ocn = "$cpp_ocn -DLEVITUS2X"
endif
if ($#turbclo != 0 || $#tracers != 0) then
  echo $SRCROOT/components/micom/trc >> Filepath
  set cpp_ocn = "$cpp_ocn -DTRC"
endif
if ($#turbclo != 0) then
  set twoeq = FALSE
  set oneeq = FALSE
  foreach option ($turbclo)
    if      ($option == twoeq) then
      set cpp_ocn = "$cpp_ocn -DTKE -DGLS"
      set twoeq = TRUE
    else if ($option == oneeq) then
      set cpp_ocn = "$cpp_ocn -DTKE"
      set oneeq = TRUE
    else if ($option == advection) then
      set cpp_ocn = "$cpp_ocn -DTKEADV"
    else if ($option == isodif) then
      set cpp_ocn = "$cpp_ocn -DTKEIDF"
    else
      echo $0": Turbulent closure option $option is not recognized!"
      exit -1
    endif
  end
  if ($twoeq == 'FALSE' && $oneeq == 'FALSE') then
    echo $0": For turbulent closure either twoeq or oneeq must be provided as options!"
    exit -1
  endif
  if ($twoeq == 'TRUE' && $oneeq == 'TRUE') then
    echo $0": Do not use both twoeq and oneeq as options for turbulent closure!"
    exit -1
  endif
endif
if ($#tracers != 0) then
  foreach module ($tracers)
    if      ($module == iage) then
      echo $SRCROOT/components/micom/idlage >> Filepath
      set cpp_ocn = "$cpp_ocn -DIDLAGE"
    else if ($module == ecosys) then
      echo $SRCROOT/componentsmicom/hamocc >> Filepath
      set cpp_ocn = "$cpp_ocn -DHAMOCC -DRESTART_BGC"
      if ($co2type == prognostic) then
        set cpp_ocn = "$cpp_ocn -DPROGCO2"
      else if ($co2type == diagnostic) then
        set cpp_ocn = "$cpp_ocn -DDIAGCO2"
      else if ($co2type != constant) then
        echo $0": CO2 type $co2type is not recognized!"
        exit -1
      endif
      if ($rivnutr == TRUE) then 
        set cpp_ocn = "$cpp_ocn -DRIV_GNEWS"
      endif 
    else
      echo $0": tracer module $module is not recognized!"
      exit -1
    endif
  end
endif

#------------------------------------------------------------------------------
# Build the library
#------------------------------------------------------------------------------

gmake complib -j $GMAKE_J MODEL=micom COMPLIB=$LIBROOT/libocn.a MACFILE=$CASEROOT/Macros.$MACH USER_CPPDEFS="$cpp_ocn" -f $CASETOOLS/Makefile || exit 2

if !(-f $LIBROOT/libocn.a) then
  echo "ERROR: micom library not available"
  exit -1
endif

